#---- query 11 -----#
# obtain the individual-level dataset
# combined with gdp in birthyear data
# similar to query 10, but without the average aggregation of occstatus
# currently querying small dataset
# endpoint = http://virtuoso.kdr250.eculture.labs.vu.nl/sparql

PREFIX sdmxdim: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX sdmxmsr: <http://purl.org/linked-data/sdmx/2009/measure#>

PREFIX ordergb: <http://data.socialhistory.org/resource/napp/ORDERGB/>
PREFIX dim: <http://data.socialhistory.org/vocab/napp/dimension/>
PREFIX country: <http://data.socialhistory.org/resource/clio/country/>
PREFIX prop: <http://data.socialhistory.org/resource/clio/property/>
PREFIX indicator: <http://data.socialhistory.org/resource/clio/indicator/>
PREFIX nappcountry: <http://data.socialhistory.org/resource/napp/BPLCNTRY/>


SELECT ?age ?birthyear ?occstatus ?gdppc ?county ?birthcountry ?maritalstat ?sex ?urban 

FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881>

WHERE {
    ?personid dim:ORDERGB ?ordergb .
    ?personid dim:AGE ?age .
    FILTER(?ordergb != ordergb:24 || ?age != '999')
    ?personid dim:YEAR ?year .

    # additional control variables, still to be de-uri'd & minus'd
    ?personid dim:COUNTYGB ?county .
    ?personid dim:BPLCNTRY ?birthcountry .
    ?personid dim:SEX ?sex .
    ?personid dim:URBAN ?urban .
    FILTER(?birthcountry != nappcountry:99999)
    ?personid dim:MARST ?maritalstat
    BIND(xsd:integer(?year) - xsd:integer(?age) AS ?birthyear) .
    BIND(xsd:integer(strafter(str(?ordergb), 'ORDERGB/')) as ?occstatus) .
    {
        SELECT ?birthyear ?gdppc 
        FROM <http://data.socialhistory.org/resource/clio/>
        WHERE {
            ?data sdmxdim:refArea country:826 .
            ?data sdmxdim:refPeriod ?year .
            ?data prop:indicator indicator:GDPPC1990GKD .
            ?data sdmxmsr:obsValue ?gdppc .
            BIND(xsd:integer(str(?year)) AS ?birthyear) .
        }
    }
}

LIMIT 100