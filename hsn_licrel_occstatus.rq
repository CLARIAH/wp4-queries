#+ summary: occupational status by religion (using licr codes) over time
#+ tags:
#+      - HSN
#+      - religion
#+      - licr
#+      - occupation
#+      - hisco
#+      - hisclass

# NB: hiscam preferable over hisclass, but timed out in earlier versions
# maybe fixable

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX hsndim: <http://data.socialhistory.org/resource/hsn/dimension/>
PREFIX hsn: <http://data.socialhistory.org/resource/hsn/>
PREFIX hsn-hisco: <http://data.socialhistory.org/resource/hsn2013a/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?occentry ?relentry ?idnrhsp ?occvlgnr ?relvlgnr ?hsnocc ?hiscoocc ?hisclass ?religie ?licrcode ?occyear ?relyear 
FROM <http://data.socialhistory.org/resource/BEVDYNAP/assertion/7da40d05/2017-03-09T14:25> 
FROM <http://data.socialhistory.org/resource/hsn2013a_hisco_comma/assertion/fbedcdf7/2016-12-12T12:49>
FROM <http://data.socialhistory.org/resource/Kerkgenootschap/assertion/42aac555/2017-03-09T14:55>

WHERE {
    ?occentry hsndim:occupationalTitle ?hsnocc .
    ?occentry hsndim:IDNRHSP ?idnrhsp .
    ?occentry hsndim:INGVLGNR ?occvlgnr .
    ?occentry hsndim:INGJAR ?occyear .

    ?hsnocc hsn-hisco:hisclass ?hisclass .

    ?relentry hsndim:IDNRHSP ?idnrhsp .
    ?relentry hsndim:INGVLGNR ?relvlgnr .
    ?relentry hsndim:Kerkgenootschap ?religie .
    ?relentry hsndim:INGJAR ?relyear .
    ?religie skos:exactMatch ?licrcode .

    FILTER(?occvlgnr = ?relvlgnr) .
}
ORDER BY ?occentry