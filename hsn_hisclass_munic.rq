#+ summary: hisclass over time for OPs with place of residence
#+ tags:
#+      - HSN

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX hsndim: <http://data.socialhistory.org/resource/hsn/dimension/>
PREFIX hsn: <http://data.socialhistory.org/resource/hsn/>
PREFIX hsn-hisco: <http://data.socialhistory.org/resource/hsn2013a/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?hisclass ?occvlgnr ?occdat ?gemeente ?adres_ingdat ?adres_utgdat ?subj ?idnr ?amco ?sexop

FROM <http://data.socialhistory.org/resource/BEVDYNAP/assertion/7da40d05/2017-05-17T20:13>
FROM <http://data.socialhistory.org/resource/BEVADRES/assertion/71e4e8db/2017-05-17T20:13>
FROM <http://data.socialhistory.org/resource/hsn2013a_hisco_comma/assertion/fbedcdf7/2016-12-12T12:49>
FROM <http://data.socialhistory.org/resource/gemeenten/assertion/0e8e6b77/2017-02-28T14:29>
FROM <http://data.socialhistory.org/resource/BEVOP/assertion/35719419/2017-05-17T20:13>

WHERE {
    ?subj hsndim:occstring_lc ?occlab .
    ?hisco skos:altLabel ?occlab .
    ?hisco hsn-hisco:hisclass ?hisclass .

    ?subj hsndim:INGVLGNR ?occvlgnr .
    ?subj hsndim:INGDAT ?occdat .
    ?subj hsndim:IDNR ?idnr .
    ?subj hsndim:HUISHNR ?huishnr .
    ?subj hsndim:DYNATYPE <http://data.socialhistory.org/resource/hsn/code/DYNATYPE/5> .
    ?subj hsndim:HSNperson ?hsnperson .

    ?opentry hsndim:HSNperson ?hsnperson.
    ?opentry hsndim:SEX_OP ?sexop .

    ?adresobs hsndim:IDNR ?idnr.
    ?adresobs hsndim:HUISHNR ?huishnr .
    ?adresobs hsndim:GEMEENTE ?gemeente . 
    ?adresobs hsndim:ADRESDAT ?adres_ingdat .
    ?adresobs hsndim:ADUITDAT ?adres_utgdat .
    FILTER(?occdat <= ?adres_utgdat && ?occdat >= ?adres_ingdat )
  
    ?gemeente skos:exactMatch ?amco
}
ORDER BY ?subj