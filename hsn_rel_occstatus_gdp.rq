#+ summary: occupational status by religion over time and gdp
#+ tags:
#+      - HSN
#+      - religion
#+      - occupation
#+      - hisco
#+      - hisclass
#+      - clio-infra
#+      - GDP

# NB: hiscam preferable over hisclass, but timed out in earlier versions
# maybe fixable

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX hsndim: <http://data.socialhistory.org/resource/hsn/dimension/>
PREFIX hsn: <http://data.socialhistory.org/resource/hsn/>
PREFIX sdmxdim: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX hsn-hisco: <http://data.socialhistory.org/resource/hsn2013a/>

SELECT ?occentry ?relentry ?idnrhsp ?occvlgnr ?relvlgnr ?hsnocc ?hisclass ?religie ?occyear ?relyear ?gdp
FROM <http://data.socialhistory.org/resource/BEVDYNAP/assertion/7da40d05/2017-03-29T18:17> 
FROM <http://data.socialhistory.org/resource/hsn2013a_hisco_comma/assertion/fbedcdf7/2016-12-12T12:49>
FROM <http://data.socialhistory.org/ns/resource/GDP_Capita-historical/assertion/fd2ce578/2016-08-25T14:25>

WHERE {
  ?occentry hsndim:occupationalTitle ?hsnocc .
  ?occentry hsndim:IDNRHSP ?idnrhsp .
  ?occentry hsndim:INGVLGNR ?occvlgnr .
  ?occentry hsndim:INGJAR ?occyear .
  
  ?hsnocc hsn-hisco:hisclass ?hisclass .
  
  ?relentry hsndim:IDNRHSP ?idnrhsp .
  ?relentry hsndim:INGVLGNR ?relvlgnr .
  ?relentry hsndim:Kerkgenootschap ?religie .
  ?relentry hsndim:INGJAR ?relyear .
  
  FILTER(?occvlgnr = ?relvlgnr) .

  ?gdpentry <http://data.socialhistory.org/resource/clio/measure/GDP_per_Capita> ?gdp .
  ?gdpentry sdmxdim:refArea <http://data.socialhistory.org/resource/clio/Netherlands/1946/2012> .
  ?gdpentry sdmxdim:refPeriod ?relyear  .
}
ORDER BY ?occentry