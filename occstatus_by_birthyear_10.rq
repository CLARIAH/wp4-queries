# ----- qry10 -------#
# average occstatus by birthyear
# linked to british gdp from clio-datacube

# goal is to get occupation status by birthyear
# ideally occupational status would come from hisco -> hiscam mapping
# and has birthyear linked to gdp data (better yet; gdp at county/region level)

# query on clio-gdp using worldbank datacube structure (more or less)
# endpoint = http://virtuoso.kdr250.eculture.labs.vu.nl/sparql


PREFIX ordergb: <http://data.socialhistory.org/resource/napp/ORDERGB/>
PREFIX sdmxdim: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX sdmxmsr: <http://purl.org/linked-data/sdmx/2009/measure#>
PREFIX country: <http://data.socialhistory.org/resource/clio/country/>
PREFIX prop: <http://data.socialhistory.org/resource/clio/property/>
PREFIX indicator: <http://data.socialhistory.org/resource/clio/indicator/>
PREFIX dim: <http://data.socialhistory.org/vocab/napp/dimension/>

SELECT DISTINCT ?birthyear ?gdp ?mean_occstatus

FROM <http://data.socialhistory.org/resource/clio/> 

WHERE {
	?dataset sdmxdim:refArea country:826 .
	?dataset sdmxdim:refPeriod ?year .
	?dataset prop:indicator indicator:GDPPC1990GKD .
	?dataset sdmxmsr:obsValue ?gdp .
	
	# difficult to remove this bind!? 
	BIND(xsd:integer(STR(?year)) AS ?birthyear)

    { SELECT (AVG(?occstatus) as ?mean_occstatus) ?birthyear
	
	FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881>
	FROM <http://data.socialhistory.org/resource/napp/>
	
		WHERE {
		?s dim:ORDERGB ?ordergb .
        ?ordergb rdf:value ?occstatus .
		?s dim:AGE ?age .
		FILTER(?occstatus != 24 || ?age != '999')
		?s dim:YEAR ?year .
		BIND(xsd:integer(?year) - xsd:integer(?age) AS ?birthyear)
      }
      GROUP BY ?birthyear
    }
}

ORDER BY ?birthyear