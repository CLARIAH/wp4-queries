#+ summary: occupational status by standarised religion and GDP
#+ tags:
#+      - HSN
#+      - religion
#+      - occupation
#+      - hisco
#+      - hisclass
#+      - GDP

# NB: hiscam preferable over hisclass, but timed out in earlier versions
# maybe fixable

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX hsndim: <http://data.socialhistory.org/resource/hsn/dimension/>
PREFIX hsn: <http://data.socialhistory.org/resource/hsn/>
PREFIX hsn-hisco: <http://data.socialhistory.org/resource/hsn2013a/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sdmxdim: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX cliomsr: <http://data.socialhistory.org/resource/clio/measure/>

SELECT *
FROM <http://data.socialhistory.org/resource/BEVDYNAP/assertion/7da40d05/2017-05-17T20:13>
FROM <http://data.socialhistory.org/resource/hsn2013a_hisco_comma/assertion/fbedcdf7/2016-12-12T12:49>
FROM <http://data.socialhistory.org/resource/Kerkgenootschap/assertion/42aac555/2017-03-09T14:55>
FROM <http://data.socialhistory.org/ns/resource/GDP_Capita-historical/assertion/fd2ce578/2016-08-25T14:25>

WHERE {
    ?indiv hsndim:individualObservation ?occobs .
    ?occobs hsndim:occupationalTitle ?occup .
    ?occobs hsndim:INGJAR ?occyear .
    ?occobs hsndim:INGVLGNR ?occno .

    ?occup hsn-hisco:hisclass ?hisclass .

    ?indiv hsndim:individualObservation ?relobs .
    ?relobs hsndim:Kerkgenootschap ?religie .
    ?relobs hsndim:INGVLGNR ?relno .

    ?religie skos:exactMatch ?licr .

    FILTER(?relno = ?occno) .

    ?gdpentry cliomsr:GDP_per_Capita ?gdp .
    ?gdpentry sdmxdim:refArea <http://data.socialhistory.org/resource/clio/Netherlands/1946/2012> .
    ?gdpentry sdmxdim:refPeriod ?occyear  .
}
