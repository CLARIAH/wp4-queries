PREFIX relate: <http://data.socialhistory.org/resource/napp/RELATE/>
PREFIX urban: <http://data.socialhistory.org/resource/napp/URBAN/>
PREFIX sex: <http://data.socialhistory.org/resource/napp/SEX/>
PREFIX napp-dimension: <http://data.socialhistory.org/vocab/napp/dimension/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?householdID ?personID ?urban_area ?region ?male ?age_child ?enrolled ?agen_head ?occupation_father ?agen_mother ?occupation_mother ?n_servants ?nchildren ?ageoldestchild ?n_older_siblings ?birth_order 

FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881>

WHERE {

    VALUES ?relate { relate:301 relate:302 relate:306 }
    ?personID napp-dimension:SERIAL ?householdID .
    ?personID napp-dimension:AGE ?age_child .
    ?personID napp-dimension:SEX ?sex .
    ?personID napp-dimension:URBAN ?urban .
    ?personID napp-dimension:COUNTYGB ?region .
    BIND(IF(?urban = urban:2, 1, 0) AS ?urban_area) .
    BIND(IF(?sex = sex:1, 1, 0) AS ?male) .
    ?personID napp-dimension:OCCSTRNG ?occstring .
    BIND(IF(REGEX(?occstring, "sch|^(sc|sh)[ao].?l|app|pup|[ (]ap[)]?$|^[(]?ap[ )]|apren|stud|apr|^sco?$|ysgol|[(]sc?r?[)]|apic|^s$", 'i'), 1, 0) AS ?enrolled) .
    ?personID napp-dimension:RELATE ?relate .

    OPTIONAL{
        ?head napp-dimension:SERIAL ?householdID .
        ?head napp-dimension:RELATE relate:101 .
        ?head napp-dimension:AGE ?agen_head .
        ?head napp-dimension:OCCSTRNG ?occupation_father .
    }

    OPTIONAL{
        VALUES ?relate_m { relate:201 relate:202 relate:203 }
        ?mother napp-dimension:SERIAL ?householdID .
        ?mother napp-dimension:AGE ?agen_mother .
        ?mother napp-dimension:OCCSTRNG ?occupation_mother .
        ?mother napp-dimension:RELATE ?relate_m .
    }

     OPTIONAL {
        SELECT ?householdID (COUNT(?servant) AS ?nservants) WHERE{
            VALUES ?relate_srv { relate:1210 relate:1211 relate:1212 relate:1213 relate:1214 relate:1215 relate:1216 relate:1217 }
            ?servant napp-dimension:SERIAL ?householdID .
            ?servant napp-dimension:RELATE ?relate_srv .
        }
        GROUP BY ?householdID
    }
    BIND ( IF (BOUND (?nservants), ?nservants, 0 )  as ?n_servants  ) .

      { 
         SELECT ?householdID (COUNT(?child) AS ?nchildren) (MAX(xsd:integer(?age)) AS ?ageoldestchild) WHERE{
            ?child napp-dimension:SERIAL ?householdID .
            ?child napp-dimension:RELATE ?relate_c .
            VALUES ?relate_c { relate:301 relate:302 relate:306 }
            ?child napp-dimension:AGE ?age .
        }
        GROUP BY ?householdID
    }

     
     OPTIONAL {
        SELECT ?personID ?householdID (COUNT(DISTINCT ?siblingID) as ?n_older_siblings) WHERE {
            VALUES ?relate { relate:301 relate:302 relate:306 }
            ?personID napp-dimension:SERIAL ?householdID . 
            ?personID napp-dimension:AGE ?age_child .
            ?siblingID napp-dimension:SERIAL ?householdID .
            ?siblingID napp-dimension:AGE ?sibling_age .
            ?siblingID napp-dimension:RELATE ?relate .
            FILTER (xsd:integer(?sibling_age) > xsd:integer(?age_child))
            FILTER (?personID != ?siblingID)
        }
        GROUP BY ?personID ?householdID
    }

    BIND ((?n_older_siblings + 1) AS ?birth_order)
} 

LIMIT 100
