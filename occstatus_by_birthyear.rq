# goal is to get occupation status by birthyear
# ideally occupational status would come from hisco -> hiscam mapping
# and has birthyear linked to gdp data (better yet; gdp at county/region level)

# ----- qry10 -------#
# average occstatus by birthyear
# linked to british gdp from clio-datacube
# qry seems to get no response

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix clio: <http://data.socialhistory.org/resource/clio/>
PREFIX dim: <http://data.socialhistory.org/vocab/napp/dimension/>
PREFIX ordergb: <http://data.socialhistory.org/resource/napp/ORDERGB/>
PREFIX age: <http://data.socialhistory.org/resource/napp/AGE/>

SELECT DISTINCT ?birthyear ?year ?gdp
FROM <http://data.socialhistory.org/resource/clio/>
WHERE {
    ?s clio:gdppc ?gdp .
    ?s clio:refPeriod ?year .
    ?s clio:refArea "826" .
    BIND(xsd:integer(STR(?year)) AS ?birthyear)
    OPTIONAL {
        SELECT (AVG(?occstatus) as ?mean_occstatus)  ?birthyear
        FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881_small> 
        WHERE{
            ?s dim:ORDERGB ?ordergb .
            MINUS{?s dim:ORDERGB ordergb:24} .
            MINUS{  ?s dim:AGE '999'} .
            ?s dim:AGE ?age .
            ?s dim:YEAR ?year .
            BIND(xsd:integer(?year) - xsd:integer(?age) AS ?birthyear)
            BIND(xsd:integer(STRAFTER(STR(?ordergb), 'ORDERGB/')) AS ?occstatus) .
        }
        GROUP BY ?birthyear
    }
}

# ------ qry 9 ------#
# query for individual-level dataset with some controls
# all these variables should also be investigated for missing codes etc.
PREFIX dim: <http://data.socialhistory.org/vocab/napp/dimension/>
PREFIX ordergb: <http://data.socialhistory.org/resource/napp/ORDERGB/>
PREFIX age: <http://data.socialhistory.org/resource/napp/AGE/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
SELECT ?year ?age ?age_num ?ordergb ?occstatus ?birthyear 
    ?county ?birthcounty ?birthcountry ?marst ?labforce ?sex ?urban
FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881_small>
WHERE{
    ?s dim:AGE ?age .
    ?s dim:ORDERGB ?ordergb .
    ?s dim:YEAR ?year .
    ?s dim:COUNTYGB ?county .
    # ?county skos:prefLabel ?countyname # vocab with preflabels not present
    ?s dim:BPLCNTRY ?birthcountry .
    ?s dim:BPLCTYGB ?birthcounty .
    ?s dim:MARST ?marst .
    ?s dim:LABFORCE ?labforce .
    ?s dim:SEX ?sex .
    ?s dim:URBAN ?urban .
    MINUS{
        {?s dim:ORDERGB ordergb:24 } UNION {?s dim:AGE "999"}
    }
    BIND(xsd:integer(STRAFTER(STR(?ordergb), 'ORDERGB/')) AS ?occstatus)
    BIND(xsd:integer(?age) AS ?age_num)
    BIND(xsd:integer(?year) - ?age_num AS ?birthyear)
}
LIMIT 10

#---- qry 8 ------#
# add number of observations to calculate standard error of the mean
# and output to simple visualisation
# might also be nice to get some indication of the spread of
# generations for each birthyear, but not sure how that would work

#---- qry 7 ------#
# get mean by age as descriptive, 
# this time excluding orderdb:24
# as well as age = 999
PREFIX dim: <http://data.socialhistory.org/vocab/napp/dimension/>
PREFIX ordergb: <http://data.socialhistory.org/resource/napp/ORDERGB/>
PREFIX age: <http://data.socialhistory.org/resource/napp/AGE/>
SELECT (AVG(?occstatus) as ?mean_occstatus)  ?birthyear
FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881_small> 
WHERE{
    ?s dim:ORDERGB ?ordergb .
    MINUS{?s dim:ORDERGB ordergb:24} .
    MINUS{  ?s dim:AGE '999'} .
    ?s dim:AGE ?age .
    ?s dim:YEAR ?year .
    BIND(xsd:integer(?year) - xsd:integer(?age) AS ?birthyear)
    BIND(xsd:integer(STRAFTER(STR(?ordergb), 'ORDERGB/')) AS ?occstatus) .
}
GROUP BY ?birthyear
ORDER BY ?birthyear

#---- qry 6 ------#
# little test-query to see how to filter/minus on literal
PREFIX dim: <http://data.socialhistory.org/vocab/napp/dimension/>
SELECT DISTINCT ?age
FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881_small> 
WHERE{
    ?s dim:AGE ?age .
    MINUS{
        ?s dim:AGE '999'
    }
}
ORDER BY ?age


#---- qry 5 ------#
# turns out 24 is the no occupation category, should be left out 
# (better yet, imputed, but let's leave that for another time)
# done through MINUS because I couldn't figure out negation on VALUES
# alternative: FILTER NOT EXISTS {?s dim:ORDERGB ordergb:24}
PREFIX dim: <http://data.socialhistory.org/vocab/napp/dimension/>
PREFIX ordergb: <http://data.socialhistory.org/resource/napp/ORDERGB/>
SELECT DISTINCT ?occstatus ?ordergb
FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881_small>
WHERE{
    ?s dim:ORDERGB ?ordergb .
    BIND(xsd:integer(STRAFTER(STR(?ordergb), 'ORDERGB/')) AS ?occstatus) .
    MINUS{
        ?s dim:ORDERGB ordergb:24
    }
}
ORDER BY ?occstatus
GROUP BY ?occstatus

#---- qry 4 ------#
# inspect data because 0 gets a (high) average occupational status
PREFIX dim: <http://data.socialhistory.org/vocab/napp/dimension/>
SELECT ?occstatus ?age
FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881_small>
WHERE{
    ?s dim:ORDERGB ?ordergb .
    BIND(xsd:integer(STRAFTER(STR(?ordergb), 'ORDERGB/')) AS ?occstatus) .
    ?s dim:AGE ?age.
    VALUES ?age{"000" "010" "020" "080"}
}
LIMIT 200
ORDER BY ?age

#---- qry3 ------#

# get mean by age as check for data
PREFIX dim: <http://data.socialhistory.org/vocab/napp/dimension/>
SELECT (ROUND(AVG(?occstatus) as ?mean_occstatus)) ?age FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881_small> WHERE{
    ?s dim:ORDERGB ?ordergb .
    ?s dim:AGE ?age .
    BIND(xsd:integer(STRAFTER(STR(?ordergb), 'ORDERGB/')) AS ?occstatus) .
}
GROUP BY ?age
ORDER BY ?age

#---- qry 2 ------#
# get ordergb by birth year
PREFIX dim: <http://data.socialhistory.org/vocab/napp/dimension/>
SELECT ?occstatus ?year ?age ?birthyear ?county FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881_small> WHERE{
    ?s dim:ORDERGB ?ordergb .
    BIND(xsd:integer(STRAFTER(STR(?ordergb), 'ORDERGB/')) AS ?occstatus)
    ?s dim:COUNTYGB ?county .
    ?s dim:YEAR ?year.
    ?s dim:AGE ?age .
    BIND(xsd:integer(?year) - xsd:integer(?age) AS ?birthyear) .
}
LIMIT 100


#---- qry 1 ------#
# see what's available to work with
SELECT DISTINCT ?p FROM <http://data.socialhistory.org/resource/napp/dataset/englandwales1881_small> WHERE{
    ?s ?p ?o
}