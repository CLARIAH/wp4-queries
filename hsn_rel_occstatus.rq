#+ summary: occupational status by religion over time
#+ tags:
#+      - HSN
#+      - religion
#+      - occupation
#+      - hisco
#+      - hisclass

# NB: hiscam preferable over hisclass, but timed out in earlier versions
# maybe fixable

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX hsndim: <http://data.socialhistory.org/resource/hsn/dimension/>
PREFIX hsn: <http://data.socialhistory.org/resource/hsn/>
PREFIX hsn-hisco: <http://data.socialhistory.org/resource/hsn2013a/>

SELECT ?occentry ?relentry ?idnrhsp ?occvlgnr ?relvlgnr ?hsnocc ?religie ?occyear ?relyear ?hiscoocc
FROM <http://data.socialhistory.org/resource/BEVDYNAP/assertion/7da40d05/2017-03-07T15:48> 
FROM <http://data.socialhistory.org/resource/hsn2013a_hisco_comma/assertion/fbedcdf7/2016-12-12T12:49>
WHERE {
    ?occentry hsn:occupationalTitle ?hsnocc .
    ?occentry hsndim:IDNRHSP ?idnrhsp .
    ?occentry hsndim:INGVLGNR ?occvlgnr .
    ?occentry hsndim:INGJAR ?occyear .

    BIND(URI(CONCAT("http://data.socialhistory.org/resource/hsn2013a/occupation/", STRAFTER(STR(?hsnocc), "2013a/"))) AS ?hiscoocc) .
    ?hiscoocc hsn-hisco:hisclass ?hisclass .

    ?relentry hsndim:IDNRHSP ?idnrhsp .
    ?relentry hsndim:INGVLGNR ?relvlgnr .
    ?relentry hsndim:Kerkgenootschap ?religie .
    ?relentry hsndim:INGJAR ?relyear .

    FILTER(?occvlgnr = ?relvlgnr) .
}
ORDER BY ?occentry