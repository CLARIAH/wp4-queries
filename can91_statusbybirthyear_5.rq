# occupational status by occupation in canada 1891
# same as occstatus_by_birthyear from engwal 1881
# but using slightly more involved occupational coding

# combine person-level obs, occupational mapping, and gdp

PREFIX napp-dimension: <http://data.socialhistory.org/vocab/napp/dimension/>
PREFIX cliomsr: <http://data.socialhistory.org/resource/clio/measure/>
PREFIX clio: <http://data.socialhistory.org/resource/clio/>
PREFIX clioctr: <http://data.socialhistory.org/resource/clio/country/>
PREFIX hisco: <http://data.socialhistory.org/vocab/hisco/>
PREFIX hiscam: <http://data.socialhistory.org/resource/hiscam/>
PREFIX sdmxd: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?person ?occhisco ?hisco ?hiscam ?gdppc ?iyear ?age

FROM <http://data.socialhistory.org/resource/napp/dataset/canada1891>
FROM <http://data.socialhistory.org/resource/occhisco2hisco/assertion/8b5a0fb3/2016-11-01T13:12>
FROM <http://data.socialhistory.org/resource/hisco_45/assertion/0187d50d/2017-01-22T05:50>
FROM <http://data.socialhistory.org/resource/hiscam_ca/assertion/58742daf/2016-12-05T10:54>
FROM <http://data.socialhistory.org/ns/resource/GDP_Capita-historical/assertion/fd2ce578/2016-08-25T14:25>

WHERE {
    # get right clio data and year for right country
    ?country cliomsr:GDP_per_Capita ?gdppc .
    ?country sdmxd:refArea ?refarea . 
    ?refarea clio:ccode clioctr:124.0 .
    ?country sdmxd:refPeriod ?year .
    BIND(xsd:int(str(?year)) AS ?iyear) .
    FILTER(?iyear < 1879) .

    OPTIONAL{
        # select variables of interest and filter out bad obs. from census
        ?person napp-dimension:AGE ?age .
        ?person napp-dimension:YEAR ?censusyear .        
        FILTER((?censusyear - ?age) = ?iyear && ?age != 999 && ?age > 12) .
        ?person napp-dimension:OCCHISCO ?occhisco .
        FILTER(?occhisco != 99999) . 

        ?occhisco (skos:exactMatch|skos:closeMatch) ?hisco .
        ?hiscam_entry hiscam:hisco ?hisco .
        ?hiscam_entry hiscam:value ?hiscam .
        ?hiscam_entry sdmxd:refArea <http://sws.geonames.org/6251999/> .
    }
}
ORDER BY ?person
LIMIT 100
