PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX iisgv: <https://iisg.amsterdam/openarchdeath/vocab/>
PREFIX gg: <http://www.gemeentegeschiedenis.nl/gg-schema#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX hdng: <https://iisg.amsterdam/hdng/>
PREFIX hdnggemeente: <https://iisg.amsterdam/hdng/gemeente/>
PREFIX hdngv: <https://iisg.amsterdam/hdng/vocab/>
PREFIX hdngvar: <https://iisg.amsterdam/hdng/variable/>
PREFIX sdmx: <http://purl.org/linked-data/sdmx/2009/vocab#>
PREFIX sdmx-code: <http://purl.org/linked-data/sdmx/2009/code#>



SELECT  * WHERE  {

 # get all available variables and details of this variable, for all municipalities
     
 ?gemeente schema:variableMeasured ?variable .   
 ?variable hdngv:category ?category .
 ?variable hdngv:specification ?specification . 
 ?variable sdmx:sex ?sex .

 # give me only variables on "[enter string here]"  
  
 FILTER(REGEX(?category, "beroeps", "i")) 
 
 # ...and for this municipality (upper case!)
 
 FILTER(?gemeente = hdnggemeente:LEIDEN)
 
 # retrieve which year observations are available for these variables for the specified municipality

 ?variable schema:hasObservation ?observation . 
 ?observation hdngv:year ?year .
 
 
 # optional: specify a certain year
 
  FILTER(?year = "1899"^^<http://www.w3.org/2001/XMLSchema#gYear>) 
  
 # now give me the actual values of these variables for this municipality#year combination  
 
 ?observation hdng:gemeente ?gemeente . 
 ?observation ?variable ?value . 
  
    }

