PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX hdng: <https://iisg.amsterdam/hdng/>
PREFIX hdngv: <https://iisg.amsterdam/hdng/vocab/>
PREFIX hdngvar: <https://iisg.amsterdam/hdng/variable/>
PREFIX gg: <http://www.gemeentegeschiedenis.nl/gg-schema#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>

SELECT ?gemeente ?gg_gemeente ?agri_workers ?population ?share_agri_workers ?wkt ?wktColor ?wktTooltip  WHERE {
 
  ?gemeente a  hdng:gemeente ;
  			   schema:hasObservation ?observation .
  ?observation hdngvar:LAX2 ?agri_workers .
  ?observation hdngvar:BEV1 ?population .
  ?observation hdngv:year "1889"^^<http://www.w3.org/2001/XMLSchema#gYear> .
  ?gemeente    gg:amsterdamCode/gg:identifies  ?gg_gemeente .
  ?gg_gemeente geo:hasGeometry/geo:asWKT ?wkt .
  
 
  FILTER(?agri_workers > 0)
  FILTER(?population > 0)
  BIND((xsd:float(?agri_workers / xsd:float(?population)*100  ) ) as ?share_agri_workers)
  BIND(concat('viridis,', str((?share_agri_workers) /21)) as ?wktColor)
  BIND(concat(xsd:integer(?share_agri_workers),'%','_', STRAFTER(STR(?gemeente), 'gemeente/')) as ?wktTooltip)
  FILTER(?share_agri_workers > 3)
  
   
} 

