# occupational status by occupation in canada 1891
# same as occstatus_by_birthyear from engwal 1881
# but using slightly more involved occupational coding

# get average hiscam value and gdp in that birthyear 
# for sumstats or visualisation

PREFIX napp-dimension: <http://data.socialhistory.org/vocab/napp/dimension/>
PREFIX clioprop: <http://data.socialhistory.org/resource/clio/property/>
PREFIX sdmx-dimension: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX sdmx-measure: <http://purl.org/linked-data/sdmx/2009/measure#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX hisco: <http://data.socialhistory.org/vocab/hisco/>
PREFIX hiscam: <http://data.socialhistory.org/vocab/hiscam/>
PREFIX qb: <http://purl.org/linked-data/cube#>

SELECT AVG(?hiscam) AS ?meanhiscam ?year ?gdppc

FROM <http://data.socialhistory.org/resource/napp/dataset/canada1891>
FROM <http://data.socialhistory.org/vocab/hisco/>
FROM <http://data.socialhistory.org/resource/gdppc/assertion/5343176a06654392f04b0dcf163d1f1f0f65ffce/2016-01-21T16:30:48.081802>

WHERE {
    ?dataset clioprop:indicator clioind:GDPPC1990GKD .
    ?dataset sdmx-dimension:refArea clioctr:124 .
    ?dataset sdmx-measure:obsValue ?gdppc .
    ?dataset sdmx-dimension:refPeriod ?year .
    FILTER(xsd:int(str(?year)) < 1892) .
    
    OPTIONAL{
		?person napp-dimension:AGE ?age .
        ?person napp-dimension:YEAR ?censusyear .
        FILTER((?censusyear - ?age) = (xsd:int(str(?year))) && ?age != 999 && ?age > 12) .
        ?person napp-dimension:OCCHISCO ?occhisco .
        FILTER(?occhisco != 99999).

        ?occhisco (skos:closeMatch|skos:exactMatch) ?hisco .
        ?hiscoentry hisco:hiscoCode ?hisco . 
        ?hiscoentry hiscam:hiscamValue ?hiscam .
        ?hiscoentry qb:refArea hiscam:CA .        
    }
}
ORDER BY ?year
LIMIT 1000
